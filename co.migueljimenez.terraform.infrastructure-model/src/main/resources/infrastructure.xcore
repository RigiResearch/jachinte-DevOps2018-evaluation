@Ecore(nsPrefix="infrastructure", nsURI="https:/migueljimenez.co/terraform/infrastructure")
@GenModel(
    bundleManifest="false",
    modelDirectory="co.migueljimenez.terraform.infrastructure-model/src/main/java-gen",
    complianceLevel="8.0"
)
package co.migueljimenez.terraform.infrastructure.model

import de.xn__ho_hia.storage_unit.StorageUnit

// Types

type StorageQuantity wraps StorageUnit<?>
type Number wraps Number

enum ContainerFormat {
	AMI as "ami" = 0
	ARI as "ari" = 1
	AKI as "aki" = 2
	BARE as "bare" = 3
	OVF as "ovf" = 4
}

enum DiskFormat {
	AMI as "ami" = 0
	ARI as "ari" = 1
	AKI as "aki" = 2
	VHD as "vhd" = 3
	VMDK as "vmdk" = 4
	RAW as "raw" = 5
	QCOW2 as "qcow2" = 6
	VDI as "vdi" = 7
	ISO as "iso" = 8
}

// Do not use a map, as there may be duplicates
class KeyValuePair<K, V> {
	K key
	V value
}

class Dictionary<K, V> {
	contains KeyValuePair<K, V>[] elements
}

// Entities

class VirtualInfrastructure {
	contains Credential[] credentials
	contains Flavor[] flavors
	contains Image[] images
	contains Instance[] instances
	contains Network[] networks
	contains SecurityGroup[] securityGroups
	contains Volume[] volumes
	contains UnknownResource<String, Object>[] resources
}

class Flavor {
	id String ^id
	String name
	int vcpus
	StorageQuantity disk
	StorageQuantity ram
}

class Image {
	id String ^id
	String name
	ContainerFormat containerFormat
	DiskFormat diskFormat
	String imageSourceUrl
	StorageQuantity minDisk
	StorageQuantity minRam
}

class Credential {
	id String ^id
	String name
	String publicKey
}

class Volume {
	id String ^id
	String name
	StorageQuantity size
	contains Image image
}

class Subnet {
	id String ^id
	String name
	String cidr
	container Network network opposite subnets
}

class Network {
	id String ^id
	String name
	contains Subnet[] subnets opposite network
}

class SecurityRule {
	int from
	int to
	String protocol
	String cidr
}

class SecurityGroup {
	id String ^id
	String name
	contains SecurityRule[] rules
}

class Instance {
	id String ^id
	String name
	contains Credential credential
	contains Flavor flavor
	contains Network[] networks
	contains Volume volume
	contains SecurityGroup[] securityGroups
}

class UnknownResource<K, V> {
	// The type of OpenStack resource
	String resourceType
	// The specific OpenStack type
	String ^type
	String name
	contains Dictionary<K, V> attributes
}
