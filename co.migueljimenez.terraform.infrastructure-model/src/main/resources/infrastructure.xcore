@Ecore(nsPrefix="infrastructure", nsURI="https:/migueljimenez.co/terraform/infrastructure")
@GenModel(
    bundleManifest="false",
    modelDirectory="co.migueljimenez.terraform.infrastructure-model/src/main/java-gen",
    complianceLevel="8.0"
)
package co.migueljimenez.terraform.infrastructure.model

import de.xn__ho_hia.storage_unit.StorageUnit

// Types

type StorageQuantity wraps StorageUnit<?>
type Number wraps Number

enum ContainerFormat {
	AMI as "ami" = 0
	ARI as "ari" = 1
	AKI as "aki" = 2
	BARE as "bare" = 3
	OVF as "ovf" = 4
}

enum DiskFormat {
	AMI as "ami" = 0
	ARI as "ari" = 1
	AKI as "aki" = 2
	VHD as "vhd" = 3
	VMDK as "vmdk" = 4
	RAW as "raw" = 5
	QCOW2 as "qcow2" = 6
	VDI as "vdi" = 7
	ISO as "iso" = 8
}

// Do not use a map, as there may be duplicates
class KeyValuePair<K, V> {
	K key
	V value
}

class Dictionary<K, V> {
	refers KeyValuePair<K, V>[] elements
}

// Entities

class VirtualInfrastructure {
	refers Credential[] credentials
	refers Flavor[] flavors
	refers Image[] images
	refers Instance[] instances
	refers Network[] networks
	refers Subnet[] subnets
	refers SecurityGroup[] securityGroups
	refers Volume[] volumes
	refers UnknownResource<String, Object>[] resources
}

class Flavor {
	id String name
	int vcpus
	StorageQuantity disk
	StorageQuantity ram
}

class Image {
	id String name
	String description
	ContainerFormat containerFormat
	DiskFormat diskFormat
	String imageSourceUrl
	StorageQuantity minDisk
	StorageQuantity minRam
}

class Credential {
	id String name
	String publicKey
}

class Volume {
	id String name
	String description
	StorageQuantity size
	refers Image image
}

class Subnet {
	id String name
	String cidr
	int ipVersion
	container Network network opposite subnets
}

class Network {
	id String name
	contains Subnet[] subnets opposite network
}

class SecurityRule {
	int from
	int to
	String protocol
	String cidr
}

class SecurityGroup {
	id String name
	String description
	refers SecurityRule[] rules
}

class Instance {
	id String name
	refers Credential credential
	refers Flavor flavor
	refers Network[] networks
	refers Volume[] volumes
	refers SecurityGroup[] securityGroups
}

class UnknownResource<K, V> {
	// The type of OpenStack resource
	String resourceType
	// The specific OpenStack type
	String ^type
	String name
	refers Dictionary<K, V> attributes
}
