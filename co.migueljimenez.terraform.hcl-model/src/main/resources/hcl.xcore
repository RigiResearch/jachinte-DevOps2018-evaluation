@Ecore(nsPrefix="hcl", nsURI="https:/migueljimenez.co/terraform/hcl")
@GenModel(
    bundleManifest="false",
    modelDirectory="co.migueljimenez.terraform.hcl-model/src/main/java-gen",
    complianceLevel="8.0"
)
package co.migueljimenez.terraform.hcl.model

import java.util.Map

// Data types

class Value {}

class KeyValuePair<K, V> wraps Map$Entry {
	K key
	V value
}

class Dictionary<T extends Value> extends Value {
	String name
	contains KeyValuePair<String, T>[] elements
}

class List extends Value {
	contains Value[] elements
}

class Number extends Value {
	int value
}

class Text extends Value {
	String value
}

class Bool extends Value {
	boolean value
}

class TextExpression extends Value {
	contains Reference expression	
}

class Reference extends Value {}

class FunctionCall extends Reference {
	String name
	contains Value[] arguments
}

class ResourceReference extends Reference {
	String[] fullyQualifiedName
}

// Entities

class Specification {
	contains Resource[] resources
}

class Resource {
	String resourceType = "resource"
	String ^type
	id String name
	contains ResourceReference[] dependsOn
	contains Dictionary<Value> attributes
}

class Variable extends Resource {
    String description
}

class Input extends Variable {
	contains Value ^default
}

class Output extends Variable {
	boolean sensitive
	contains Value value
}
